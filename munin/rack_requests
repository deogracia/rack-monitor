#!/usr/bin/perl
#
# Plugin to monitor requests per second of a rack server process.
#
# This configuration section shows the defaults of the plugin:
#
#   [rack_*]
#      env.url   http://127.0.0.1:%d/status
#      env.ports 80
#
# Parameters supported:
#
#   config   (required)
#
# Magic markers - optional - used by installation scripts and munin-config:
#
#%# family=manual

my $ret = undef;

if (! eval "require LWP::UserAgent;")
{
    $ret = "LWP::UserAgent not found";
}

my $URL = exists $ENV{'url'} ? $ENV{'url'} : "http://127.0.0.1:%d/rack_status";
my @PORTS = exists $ENV{'ports'} ? split(' ', $ENV{'ports'}) : (80);

if ( defined $ARGV[0] and $ARGV[0] eq "autoconf" )
{
    if ($ret)
    {
        print "no ($ret)\n";
        exit 1;
    }

    my $ua = LWP::UserAgent->new(timeout => 30);

    my @badports;
    foreach my $port (@PORTS) {
        my $url = sprintf $URL, $port;
        my $response = $ua->request(HTTP::Request->new('GET',$url));
        push @badports, $port unless $response->is_success and $response->content =~ /^\[Version:rack_info\]/m;
    }
    if (@badports) {
        print "no (no rack server status on ports @badports)\n";
        exit 1;
    } else {
        print "yes\n";
        exit 0;
    }
}

if ( defined $ARGV[0] and $ARGV[0] eq "config" )
{
    print "graph_title Rack requests\n";
    print "graph_args --base 1000\n";
    print "graph_vlabel requests / \${graph_period}\n";
    if (scalar(@PORTS) > 1) {
        print "graph_total total\n";
    }
    print "graph_category Http\n";
    foreach my $port (@PORTS) {
        print "requests$port.label port $port\n";
        print "requests$port.type DERIVE\n";
        print "requests$port.max 1000000\n";
        print "requests$port.min 0\n";
    }
    exit 0;
}

my $ua = LWP::UserAgent->new(timeout => 30);

foreach my $port (@PORTS) {
    my $url = sprintf $URL, $port;
    my $response = $ua->request(HTTP::Request->new('GET',$url));
    if ($response->content =~ /^\[Request:count\].*:\s+(.+)$/im) {
        print "requests$port.value $1\n";
    } else {
        print "requests$port.value U\n";
    }
}
